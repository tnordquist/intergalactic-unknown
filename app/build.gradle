apply plugin: 'com.android.application'
apply plugin: 'androidx.navigation.safeargs'

android {

    compileSdkVersion = 30

    defaultConfig {
        applicationId "edu.cnm.deepdive.appstarter"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    //noinspection GroovyAssignabilityCheck
    buildFeatures {
        //noinspection GroovyAssignabilityCheck
        viewBinding true
    }

}

dependencies {

    // .jar-based libraries included in project
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Basic Jetpack support
    implementation 'androidx.core:core:1.3.2'
    implementation 'androidx.media:media:1.3.1'
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'

    // Basic Android components
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.recyclerview:recyclerview:1.2.0"
    implementation "androidx.recyclerview:recyclerview-selection:1.1.0"

    // Fragment & navigation dependencies
    implementation 'androidx.fragment:fragment:1.3.3'
    implementation "androidx.navigation:navigation-fragment:$navigationVersion"
    implementation "androidx.navigation:navigation-ui:$navigationVersion"

    // Lifecycle (LiveData and ViewModel) dependencies
    def lifecycleVersion = '2.3.1'
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"

    // Material Design components
    implementation 'com.google.android.material:material:1.3.0'

    // ReactiveX dependencies
    implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // Room components
    def roomVersion = '2.3.0'
    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"

    // Gson (Google JSON parser) dependency
    implementation 'com.google.code.gson:gson:2.8.6'

    // Retrofit (REST client) with ReactiveX & Gson integration
    def retrofitVersion = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    // OkHttp logging dependency
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'

    // Stetho (inspection) dependency
    implementation 'com.facebook.stetho:stetho:1.5.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

}